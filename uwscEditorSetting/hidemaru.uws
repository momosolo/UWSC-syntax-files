///////////////////////////////////////////////////////////////////////////////
// 秀丸エディタ
// http://hide.maruo.co.jp/software/hidemaru.html

class hidemaru
TEXTBLOCK header
// UWSC用強調表示定義ファイル
//  Ver [uwscVersion] uwsc.chmより抜粋
ENDTEXTBLOCK

	const directory = "強調表示ファイル（秀丸エディタ用）"
	const fileName = "uwsc.hilight"
	const hilightPath = directory + "\" + fileName
	const openMode = F_WRITE1 or F_NOCR
	dim fid
	
	procedure openFile;  fid = fopen(hilightPath, openMode); fend
	procedure closeFile; fclose(fid); fend
	
	procedure putHeader
		fput(fid, replace(header, "[uwscVersion]", uwscVersion))
	fend
	procedure putFooter; ; fend
	
	procedure putBody
		hilight=""
		
		for index = 0 to length(syntaxData)-1
			csvArray = split(syntaxData[index, HASH_VAL], ",", false, false)
			syntax = csvArray[0]
			category1 = csvArray[1]
			category2 = csvArray[2]
			
			select category1
				case "スクリプト構文"
					hilight = hilight + "41," + syntax + crlf
				case "スクリプト関数"
					hilight = hilight + "105," + syntax + crlf
				case "特殊変数", "配列変数"
					hilight = hilight + "169," + syntax + crlf
				default //"定数"
					hilight = hilight + "233," + syntax + crlf
			selend
		next
		
		fput(fid, hilight)
	fend
endclass

// カラフル版
// 強調用数値一覧（すべて大文字／小文字の区別なし）
// 単・・・単語の検索    正・・・正規表現
//           [----][単--][--正][単正]
// 強調1         9    41    25    57
// 強調2        73   105    89   121
// 強調3       137   169   153   185
// 強調4       201   233   217   249
// 強調5       267   299   283   315
// 強調6       331   363   347   379
// 強調7       395   427   411   443
// 強調8       459   491   475   507
// 行の強調1    13    45    29    61
// 行の強調2    77   109    93   125
// 行の強調3   141   173   157   189
// 行の強調4   205   237   221   253
// 特に強調1    11    43    27    59
// 特に強調2    75   107    91   123
// 特に強調3   139   171   155   187
// 特に強調4   203   235   219   251

class hidemaruColorful
TEXTBLOCK header
// UWSC用強調表示定義ファイル（カラフル版）
//  Ver [uwscVersion] uwsc.chmより抜粋
//  ・強調表示/複数行の強調/ツリー定義に対応
ENDTEXTBLOCK
TEXTBLOCK footer
25,[^ 　.,+*=<>()!?\t]*(?=\()
185,\$[0-9a-f]+
201,[
201,]
395,(
395,)
395,!
395,*
395,/
395,+
395,-
395,=
411,<(=|>)?(?!(#CR>|#DBL>|#TAB>))
411,(?<!(<#CR|<#DBL|<#TAB|<))>=?
427,_
427,;
411,(?<!\d+)\.(?!\d+)
157,(?<=^[ 　\t]*)(PROCEDURE|FUNCTION)
221,(?<=^[ 　\t]*)(CLASS|MODULE)
43,<#CR>
43,<#DBL>
43,<#TAB>

//  文字列,コメント定義
/<HilightMulti>
/0014,00000000,00,00,00000004,"textblock",00000004,"endtextblock"
/0003,00000003,00,00,00000000,"//",00000002,""
/0014,00000004,00,00,00010001,"\\\"[^\"]*\\\"",00000000,"\""

//  ツリー表示用
/<TreeWord>
/0000,0002,0000,00000000,00010911,"(?<=^[ 　\\t]*)(module|class)",""
/0001,0002,0000,00000000,10010911,"(?<=^[ 　\\t]*)(procedure|function)",""
ENDTEXTBLOCK

	const directory = "強調表示ファイル（秀丸エディタ用）"
	const fileName = "uwscColorful.hilight"
	const hilightPath = directory + "\" + fileName
	const openMode = F_WRITE1 or F_NOCR
	dim fid
	
	procedure openFile;  fid = fopen(hilightPath, openMode); fend
	procedure closeFile; fclose(fid); fend
	
	procedure putHeader
		fput(fid, replace(header, "[uwscVersion]", uwscVersion))
	fend
	procedure putFooter; fput(fid, footer + crlf); fend
	
	procedure putBody
		hilight=""
		
		for index = 0 to length(syntaxData)-1
			csvArray = split(syntaxData[index, HASH_VAL], ",", false, false)
			syntax = csvArray[0]
			category1 = csvArray[1]
			category2 = csvArray[2]
			
			select category1
				case "スクリプト関数"
					hilight = hilight + "105," + syntax + crlf
				case "定数"
					hilight = hilight + "169," + syntax + crlf
				case "特殊変数", "配列変数"
					hilight = hilight + "233," + syntax + crlf
				default //"スクリプト構文"
					select category2
						case "制御文"
							hilight = hilight + "363," + syntax + crlf
						case "組込み定数"
							hilight = hilight + "169," + syntax + crlf
						case "関数の定義", "MODULE化"
							hilight = hilight + "107," + syntax + crlf
						case "演算式"
							hilight = hilight + "427," + syntax + crlf
						default
							hilight = hilight + "299," + syntax + crlf
					selend
			selend
		next
		
		fput(fid, hilight)
	fend
endclass
